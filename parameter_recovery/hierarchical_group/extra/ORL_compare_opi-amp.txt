model {

  ###### Priors ################
  alpha_a_rew ~  dnorm(0,1)
  mu_a_rew ~ dnorm(0,1)
  lambda_a_rew_opi ~ dgamma(.1,.1) 
  lambda_a_rew_amp ~ dgamma(.1,.1) 
  
  alpha_a_pun ~  dnorm(0,1)
  mu_a_pun ~ dnorm(0,1)
  lambda_a_pun_opi ~ dgamma(.1,.1) 
  lambda_a_pun_amp ~ dgamma(.1,.1) 

  alpha_K ~  dnorm(0,1)
  mu_K ~ dnorm(0,1)
  lambda_K_opi ~ dgamma(.1,.1) 
  lambda_K_amp ~ dgamma(.1,.1) 
  
  alpha_omega_f ~  dnorm(0,1)
  mu_omega_f ~ dnorm(0,1)
  lambda_omega_f_opi ~ dgamma(.1,.1) 
  lambda_omega_f_amp ~ dgamma(.1,.1) 
  
  alpha_omega_p ~  dnorm(0,1)
  mu_omega_p ~ dnorm(0,1)
  lambda_omega_p_opi ~ dgamma(.1,.1) 
  lambda_omega_p_amp ~ dgamma(.1,.1) 

  ###################################################
  #-------- opioid model----------------------------
  ###################################################
  for (s in 1:nsubs_opi) {

    a_rew_opi[s] ~ dnorm((mu_a_rew - (alpha_a_rew/2)),lambda_a_rew_opi)T(0,1)
    a_pun_opi[s] ~ dnorm((mu_a_pun - (alpha_a_pun/2)),lambda_a_pun_opi)T(0,1)
    K_opi[s]  ~ dnorm((mu_K - (alpha_K/2)),lambda_K_opi)T(0,)
    theta_opi[s] <- 1# ~ dunif(0,2)
    omega_f_opi[s] ~ dnorm((mu_omega_f - (alpha_omega_f/2)),lambda_omega_f_opi)
    omega_p_opi[s] ~ dnorm((mu_omega_p - (alpha_omega_p/2)),lambda_omega_p_opi)
  
    #------ set t1 model variables --------------------------
      
    Ev_opi[s,1,1] <- 0
    Ev_opi[s,1,2] <- 0
    Ev_opi[s,1,3] <- 0
    Ev_opi[s,1,4] <- 0
      
    Ef_opi[s,1,1] <- 0
    Ef_opi[s,1,2] <- 0
    Ef_opi[s,1,3] <- 0
    Ef_opi[s,1,4] <- 0
      
    PS_opi[s,1,1] <- 0
    PS_opi[s,1,2] <- 0
    PS_opi[s,1,3] <- 0
    PS_opi[s,1,4] <- 0

    p_opi[s,1,1] <- 0
    p_opi[s,1,2] <- 0
    p_opi[s,1,3] <- 0
    p_opi[s,1,4] <- 0

    for (t in 2:ntrials_opi[s]) {
      
      #this is imortant mention this as constructing model
      signX_opi[s,t] <- ifelse(X_opi[s,t-1]<0,-1,1)
      
      for (d in 1:4) {
        
        # -------- Updating expected values ------------------------
        Ev_update_opi[s,t,d] <- ifelse(X_opi[s,t-1]>=0,
                                  Ev_opi[s,t-1,d] + a_rew_opi[s]*((X_opi[s,t-1]) - Ev_opi[s,t-1,d]), 
                                  Ev_opi[s,t-1,d] + a_pun_opi[s]*((X_opi[s,t-1]) - Ev_opi[s,t-1,d])
        )
                              
        Ev_opi[s,t,d] <- ifelse(d==x_opi[s,t-1],Ev_update_opi[s,t,d],Ev_opi[s,t-1,d])
        
        # -------- Updating expected frequencies ------------------------
        #update expected frequencies for ALL decks - AS IF THERE WERE ALL CHOSEN
        Ef_cho_opi[s,t,d] <- ifelse(X_opi[s,t-1]>=0, 
                                Ef_opi[s,t-1,d] + a_rew_opi[s]*(signX_opi[s,t] - Ef_opi[s,t-1,d]),
                                Ef_opi[s,t-1,d] + a_pun_opi[s]*(signX_opi[s,t] - Ef_opi[s,t-1,d])
        )
        
        #update expected frequencies for ALL decks - AS IF THEY WERE ALL UNCHOSEN. 
        Ef_not_opi[s,t,d] <- ifelse(X_opi[s,t-1]>=0, 
                                Ef_opi[s,t-1,d] + a_pun_opi[s]*(-(signX_opi[s,t]/3) - Ef_opi[s,t-1,d]),
                                Ef_opi[s,t-1,d] + a_rew_opi[s]*(-(signX_opi[s,t]/3) - Ef_opi[s,t-1,d])
        ) 
        
        #copy appropriate values to ef variable
        Ef_opi[s,t,d] <- ifelse(d==x_opi[s,t-1],Ef_cho_opi[s,t,d],Ef_not_opi[s,t,d])  
        #------------------------------------------------------------------
        
        #ifelse needed to disctiminate chosen and unchosen decks
        PS_opi[s,t,d] <- ifelse(x_opi[s,t-1]==d,1/(1+K_opi[s]),PS_opi[s,t-1,d]/(1+K_opi[s]))
        
        V_opi[s,t,d] <- Ev_opi[s,t,d] + Ef_opi[s,t,d]*omega_f_opi[s] + PS_opi[s,t,d]*omega_p_opi[s]
        
        exp_p_opi[s,t,d] <- exp(theta_opi[s]*V_opi[s,t,d])
        
      }
      
      for (d in 1:4) {
        p_opi[s,t,d] <- exp_p_opi[s,t,d]/sum(exp_p_opi[s,t,])
      }
        
      x_opi[s,t] ~ dcat(p_opi[s,t,])
      
    }
  }

  ###################################################
  #-------- amphetamine model------------------------
  ###################################################
  for (s in 1:nsubs_amp) {

    a_rew_amp[s] ~ dnorm((mu_a_rew + (alpha_a_rew/2)),lambda_a_rew_amp)T(0,1)
    a_pun_amp[s] ~ dnorm((mu_a_pun + (alpha_a_pun/2)),lambda_a_pun_amp)T(0,1)
    K_amp[s]  ~ dnorm((mu_K + (alpha_K/2)),lambda_K_amp)T(0,)
    theta_amp[s] <- 1# ~ dunif(0,2)
    omega_f_amp[s] ~ dnorm((mu_omega_f + (alpha_omega_f/2)),lambda_omega_f_amp)
    omega_p_amp[s] ~ dnorm((mu_omega_p + (alpha_omega_p/2)),lambda_omega_p_amp)
  
    #------ set t1 model variables --------------------------
      
    Ev_amp[s,1,1] <- 0
    Ev_amp[s,1,2] <- 0
    Ev_amp[s,1,3] <- 0
    Ev_amp[s,1,4] <- 0
      
    Ef_amp[s,1,1] <- 0
    Ef_amp[s,1,2] <- 0
    Ef_amp[s,1,3] <- 0
    Ef_amp[s,1,4] <- 0
      
    PS_amp[s,1,1] <- 0
    PS_amp[s,1,2] <- 0
    PS_amp[s,1,3] <- 0
    PS_amp[s,1,4] <- 0

    p_amp[s,1,1] <- 0
    p_amp[s,1,2] <- 0
    p_amp[s,1,3] <- 0
    p_amp[s,1,4] <- 0

    for (t in 2:ntrials_amp[s]) {
      
      #this is imortant mention this as constructing model
      signX_amp[s,t] <- ifelse(X_amp[s,t-1]<0,-1,1)
      
      for (d in 1:4) {
        
        # -------- Updating expected values ------------------------
        Ev_update_amp[s,t,d] <- ifelse(X_amp[s,t-1]>=0,
                                  Ev_amp[s,t-1,d] + a_rew_amp[s]*((X_amp[s,t-1]) - Ev_amp[s,t-1,d]), 
                                  Ev_amp[s,t-1,d] + a_pun_amp[s]*((X_amp[s,t-1]) - Ev_amp[s,t-1,d])
        )
                              
        Ev_amp[s,t,d] <- ifelse(d==x_amp[s,t-1],Ev_update_amp[s,t,d],Ev_amp[s,t-1,d])
        
        # -------- Updating expected frequencies ------------------------
        #update expected frequencies for ALL decks - AS IF THERE WERE ALL CHOSEN
        Ef_cho_amp[s,t,d] <- ifelse(X_amp[s,t-1]>=0, 
                                Ef_amp[s,t-1,d] + a_rew_amp[s]*(signX_amp[s,t] - Ef_amp[s,t-1,d]),
                                Ef_amp[s,t-1,d] + a_pun_amp[s]*(signX_amp[s,t] - Ef_amp[s,t-1,d])
        )
        
        #update expected frequencies for ALL decks - AS IF THEY WERE ALL UNCHOSEN. 
        Ef_not_amp[s,t,d] <- ifelse(X_amp[s,t-1]>=0, 
                                Ef_amp[s,t-1,d] + a_pun_amp[s]*(-(signX_amp[s,t]/3) - Ef_amp[s,t-1,d]),
                                Ef_amp[s,t-1,d] + a_rew_amp[s]*(-(signX_amp[s,t]/3) - Ef_amp[s,t-1,d])
        ) 
        
        #copy appropriate values to ef variable
        Ef_amp[s,t,d] <- ifelse(d==x_amp[s,t-1],Ef_cho_amp[s,t,d],Ef_not_amp[s,t,d])  
        #------------------------------------------------------------------
        
        #ifelse needed to disctiminate chosen and unchosen decks
        PS_amp[s,t,d] <- ifelse(x_amp[s,t-1]==d,1/(1+K_amp[s]),PS_amp[s,t-1,d]/(1+K_amp[s]))
        
        V_amp[s,t,d] <- Ev_amp[s,t,d] + Ef_amp[s,t,d]*omega_f_amp[s] + PS_amp[s,t,d]*omega_p_amp[s]
        
        exp_p_amp[s,t,d] <- exp(theta_amp[s]*V_amp[s,t,d])
        
      }
      
      for (d in 1:4) {
        p_amp[s,t,d] <- exp_p_amp[s,t,d]/sum(exp_p_amp[s,t,])
      }
        
      x_amp[s,t] ~ dcat(p_amp[s,t,])
      
    }
  }


}