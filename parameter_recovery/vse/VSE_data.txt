model {

  theta ~ dunif(0,1)
  delta ~ dunif(0,1)
  alpha ~ dunif(0,1)
  phi ~ dnorm(0,.01)
  c <- 1#~ dunif(0,5)

  #------ set t1 model variables --------------------------
    
  exploit[1,1] <- 0
  exploit[1,2] <- 0
  exploit[1,3] <- 0
  exploit[1,4] <- 0

  explore[1,1] <- 0
  explore[1,2] <- 0
  explore[1,3] <- 0
  explore[1,4] <- 0

  p[1,1] <- 0
  p[1,2] <- 0
  p[1,3] <- 0
  p[1,4] <- 0

  for (t in 2:ntrials) {
    
    # equation 1 - no prospect theory - discuss
    # efficient code, R and L are indexed by choice on last trial
    #v[t] <- (R[t,x[t-1]]^theta) - (L[t,x[t-1]]^theta)
    #CHANGED TO FIT WITH DATA FORMAT
    v[t] <- (R[t]^theta) - (L[t]^theta)
    
    for (d in 1:4) {
      
      #---equation 2--- update exploitation. 
      exploit[t,d] <- ifelse(d==x[t-1],
                             exploit[t-1,d]*delta+v[t],
                             exploit[t-1,d]*delta)
      #---equation 3--- 
      explore[t,d] <- ifelse(d==x[t-1],
                             0,
                             explore[t-1,d] + (alpha*(phi-explore[t-1,d])))
      
      #---equation 4--- 
      exp_p[t,d] <- exp((explore[t,d]+exploit[t,d])*c)
      
    }
    
    for (d in 1:4) {
      p[t,d] <- exp_p[t,d]/sum(exp_p[t,])
    }
      
    x[t] ~ dcat(p[t,])
    
  }
}