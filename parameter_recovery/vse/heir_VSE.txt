model {

  mu_theta ~ dnorm(0,1)T(0,)
  mu_delta ~ dnorm(0,1)T(0,1)
  mu_alpha ~ dnorm(0,1)T(0,1)
  mu_phi ~ dnorm(0,.1)
  mu_c ~ dnorm(0,.1)T(0,)

  lambda_theta ~ dgamma(.01,.01)
  lambda_delta ~ dgamma(.01,.01)
  lambda_alpha ~ dgamma(.01,.01)
  lambda_phi ~ dgamma(.01,.01)
  lambda_c ~ dgamma(.01,.01)

  for (s in 1:nsubs) {

    theta[s] ~ dnorm(mu_theta,lambda_theta)
    delta[s] ~ dnorm(mu_delta,lambda_delta)
    alpha[s] ~ dnorm(mu_alpha,lambda_alpha)
    phi[s] ~ dnorm(mu_phi,lambda_phi)
    c[s] ~ dnorm(mu_c,lambda_c)

    #------ set t1 model variables --------------------------
      
    exploit[s,1,1] <- 0
    exploit[s,1,2] <- 0
    exploit[s,1,3] <- 0
    exploit[s,1,4] <- 0
  
    explore[s,1,1] <- 0
    explore[s,1,2] <- 0
    explore[s,1,3] <- 0
    explore[s,1,4] <- 0
  
    for (t in 2:ntrials[s]) {
      
      # equation 1 - no prospect theory - discuss
      # CHANGED FROM SINGLE SUB - R AND L ARE VECTORS WHEN APPLIED TO DATA
      #v[s,t] <- (R[s,t,x[s,t-1]]^theta[s]) - (L[s,t,x[s,t-1]]^theta[s])
      v[s,t] <- (R[s,t]^theta[s]) - (L[s,t]^theta[s])
      
      for (d in 1:4) {
        
        #---equation 2--- update exploitation. 
        exploit[s,t,d] <- ifelse(d==x[s,t-1],
                               exploit[s,t-1,d]*delta[s]+v[s,t],
                               exploit[s,t-1,d]*delta[s])
        #---equation 3--- 
        explore[s,t,d] <- ifelse(d==x[s,t-1],
                               0,
                               explore[s,t-1,d] + (alpha[s]*(phi[s]-explore[s,t-1,d])))
        
        #---equation 4--- 
        exp_p[s,t,d] <- exp((explore[s,t,d]+exploit[s,t,d])*c[s])
        
      }
      
      for (d in 1:4) {
        p[s,t,d] <- exp_p[s,t,d]/sum(exp_p[s,t,])
      }
        
      x[s,t] ~ dcat(p[s,t,])
      
    }
  }
}